import AlgebraInLean.Basic

namespace Defs

namespace Subgroups

-- If G is a group, then a subgroup H of G is a subset of G that is itself a group under G's
-- group operation satisfying three properties.
-- 1. The identity in G is the identity in H (H is therefore nonempty)
-- 2. ‚àÄ a, b ‚àà H then Œº a b ‚àà H
-- 3. ‚àÄ a ‚àà H, then Œπ a ‚àà H
-- Subgroups are denoted by the less than or equal sign, so we write H ‚â§ G. We encode a subgroup
-- as a Lean structure, notably not as a type class to emphasize that subgroups are simply
-- subsets of groups satisfying specific properties.
structure Subgroup (G : Type*) [Group G] where
  -- We represent the subgroup's corresponding set using Mathlib's `Set` type. Upon viewing the
  -- Mathlib documentation for the set (if you are viewing this file in Visual Studio Code, you
  -- may Ctrl-Click on the keyword to consult its definiton), we see that it is merely a wrapper
  -- for `G ‚Üí Prop`, meaning it is a function that determines what is and is not in the set.
  carrier : Set G
  -- This proposition asserts that the group is nonempty, namely that the subgroup contains the
  -- identity of G.
  nonempty : ùïñ ‚àà carrier
  -- The below propositions assert that the subgroup is closed under the group operation Œº and
  -- the inverse function Œπ.
  mul_closure : ‚àÄ {a b : G}, a ‚àà carrier ‚Üí b ‚àà carrier ‚Üí Œº a b ‚àà carrier
  inv_closure : ‚àÄ {a : G}, a ‚àà carrier ‚Üí Œπ a ‚àà carrier

variable {G : Type*} [Group G]

-- This instance coerces `Subgroup G` to `Set G`.
instance : Coe (Subgroup G) (Set G) := ‚ü®Œª H ‚Ü¶ H.carrier‚ü©
-- This instance permits the use of `H : Subgroup G`. An element `a : H`, will have two
-- properties: `a.val`, which is of type `G`, and `a.property`, which is the hypothesis that
-- `a.val ‚àà H`.
instance : CoeSort (Subgroup G) (Type _) := ‚ü®Œª H ‚Ü¶ H.carrier‚ü©
-- For more information on coercions, consult the link below.
-- https://lean-lang.org/functional_programming_in_lean/type-classes/coercion.html

-- This notation allows us to use the element-of symbol (‚àà) for subgroups.
instance : Membership G (Subgroup G) := ‚ü®Œª g H ‚Ü¶ g ‚àà H.carrier‚ü©

-- This replaces instances of `H.carrier` with `‚ÜëH` in the infoview
attribute [coe] Subgroup.carrier

-- The instances above allow us to assert that `H : Subgroup G` is itself a group. We do this by
-- implementing our `Group` interface on all `H`. As you complete the proofs yourself, you will
-- notice that many of the properties are inherited from the parent group's structure, so the
-- mere assertion of closure of H under Œº and Œπ are sufficient to prove that H is a group!
instance {H : Subgroup G} : Group H where
  -- Our operation now needs to be of type H ‚Üí H ‚Üí H instead of G ‚Üí G ‚Üí G. The ‚ü® ‚ü© notation
  -- divides the subgroup elements into its two properties.
  op := Œª ‚ü®a, ha‚ü© ‚ü®b, hb‚ü© ‚Ü¶ by
    have Œº_closed : Œº a b ‚àà H
    -- SOLUTION
    ¬∑ exact H.mul_closure ha hb

    -- Create an element of H from G, again using ‚ü® ‚ü© notation.
    exact ‚ü®Œº a b, Œº_closed‚ü©

  op_assoc := by
    -- Hint: you can use ‚ü® ‚ü© notation in `intro` as well!
    intro a b c
    ext
    apply op_assoc

  -- Make sure to provide both an element e : G and a proof that e ‚àà H.
  id := ‚ü®ùïñ, H.nonempty‚ü©

  -- Recall that the next two fields are proofs. If you ever forget the type signature of a
  -- structure field, you may either scroll to consult the definition, or alternatively, if one
  -- is viewing this document in Visual Studio Code, one may hover over the name of the field.
  id_op := by
    intro a
    ext
    apply id_op

  op_id := by
    intro a
    ext
    apply op_id

  -- Similarly to what we did above for `op`, we must show that `inv` is also closed.
  inv := Œª ‚ü®a, ha‚ü© ‚Ü¶ by

    have Œπ_closed : Œπ a ‚àà H
    -- SOLUTION
    ¬∑ exact H.inv_closure ha

    exact ‚ü®Œπ a, Œπ_closed‚ü©

  inv_op := by
    intro a
    ext
    apply inv_op

-- The largest possible subgroup of G contains every element of G. We call this subgroup as
-- `Maximal`, and it represents the specific case H ‚â§ G where H = G.
def Maximal (G : Type*) [Group G] : Subgroup G where
  carrier := Set.univ

  -- Try to come up with one-line solutions for each of the below proofs
  --PROOFS BELOW ARE SOLUTIONS
  nonempty := by
    exact trivial

  mul_closure := by
    exact Œª _ _ ‚Ü¶ trivial

  inv_closure := by
    exact Œª _ ‚Ü¶ trivial

-- The smallest possible subgroup of G is called the _trivial_ subgroup. What would this
-- smallest subgroup be? To avoid confusion with the already defined `trivial` in Lean, we will
-- call this `Minimal`.
def Minimal (G : Type*) [Group G] : Subgroup G where
  -- BELOW ARE SOLUTIONS
  carrier := {ùïñ}
  nonempty := by
    trivial
  mul_closure := by
    intro a b ha hb
    rw [ha, hb, op_id]
    trivial
  inv_closure := by
    intro a ha
    rw [ha, inv_id]
    trivial

-- This theorem here is an _extensionality_ theorem, which enables us to use the `ext` tactic on
-- equality of subgroups.
@[ext]
theorem ext (H K : Subgroup G) (h : H.carrier = K.carrier) : H = K := by
  cases H
  cases K
  congr
  done

-- We have defined a subgroup to be a subset of a group closed underoperation and three
-- additional properties. However, to show H is a subgroup of G it suffices to show two things:
-- 1. H ‚â† {‚àÖ}
-- 2. for all x, y ‚àà H, Œº x (Œπ y) ‚àà H
-- That is, a subset H of G is a subgroup IFF the two properties above hold, this is known as
-- the `Subgroup Criterion`.
-- Notice that the language nonempty we used to define a subgroup earlier may be slightly
-- misleading. Earlier, we asserted nonempty by claiming the identity existed in the subset.
-- However, this criterion truly only requires the subset to be nonempty-then the second
-- condition can be used to show that the identity must be in the subgroup. The proof is
-- outlined below and each thing to show (nonempty, inv_closure, mul_closure)
-- follows from the last.
def Subgroup_Criterion (S : Set G) (he : ‚àÉ s : G, s ‚àà S) (hc : ‚àÄ x y, x ‚àà S ‚Üí y ‚àà S ‚Üí (Œº x (Œπ y)) ‚àà S) : Subgroup G where
  carrier := S
  nonempty := by
    obtain ‚ü®s, hs‚ü© := he
    rw [‚Üê op_inv s]
    apply hc <;> exact hs
  inv_closure := by
    intro a
    have hc2 := hc
    specialize hc2 ùïñ a
    rw [‚Üê id_op (Œπ a)]
    apply hc2
    have h1 : ùïñ ‚àà S := by
      obtain ‚ü®s, hs‚ü© := he
      rw [‚Üê op_inv s]
      apply hc <;> exact hs
    exact h1
  mul_closure := by
    intro a b ha hb
    have hc3 := hc
    have hc4 := hc
    specialize hc3 a (Œπ b)
    have ht : Œπ (Œπ b) = b := sorry -- FIXME use inverse of inverse equals self
    rw [ht] at hc3
    have hf : b ‚àà S ‚Üí Œπ b ‚àà S := by
      intro hb
      rw [‚Üê id_op (Œπ b)]
      specialize hc4 ùïñ b
      apply hc4
      have h1 : ùïñ ‚àà S := by
        obtain ‚ü®s, hs‚ü© := he
        rw [‚Üê op_inv s]
        apply hc <;> exact hs
      exact h1
      exact hb
    apply hf at hb
    apply hc3 at ha
    apply ha at hb
    exact hb

-- An important property of subroups is that for any group G with subgroup H, and K a subgroup
-- of H (note this works since H itself is a group) then it must be that K is a subgroup of G.
-- That is, transitivity for subgroups holds and K ‚â§ H ‚â§ G ‚Üí K ‚â§ G.
def subgroup_trans (H : Subgroup G) (K : Subgroup H) : Subgroup G where
  carrier := {g : G | ‚àÉ h : H, h ‚àà K.carrier ‚àß g = h}
  nonempty := by
    use (ùïñ : H)
    constructor
    ¬∑ exact K.nonempty
    ¬∑ rfl
  mul_closure := by
    intros x y hx hy
    obtain ‚ü®hx, hxK, x_eq‚ü© := hx
    obtain ‚ü®hy, hyK, y_eq‚ü© := hy
    use (Œº hx hy : H)
    constructor
    ¬∑ exact K.mul_closure hxK hyK
    ¬∑ rw [x_eq, y_eq]
      rfl
  inv_closure := by
    intros x hx
    obtain ‚ü®hx, hxK, xhx‚ü© := hx
    use (Œπ (hx) : H)
    constructor
    ¬∑ exact K.inv_closure hxK
    ¬∑ rw [xhx]
      rfl

-- An extension of this transitivity that may be useful is considering three subgroups K, J, L
-- of G. It follows that if K ‚â§ J and J ‚â§ L then K ‚â§ L. Try proving this one yourself.
theorem sgp_trans [Group G] (J K L : Subgroup G) (kj : K.carrier ‚äÜ J.carrier) (jl : J.carrier ‚äÜ L.carrier) : K.carrier ‚äÜ L.carrier := by
  --EXERCISE
  intros x hx
  apply jl
  apply kj
  exact hx

-- Subgroups also hold under intersection. That is, given two subgroups H and K of a group G,
-- H ‚à© K is also a subgroup of G. Let's prove it.
def subgroup_intersection [Group G] {H K : Set G} (hH : Subgroup G) (hK : Subgroup G) (hHset : H = hH.carrier) (hKset : K = hK.carrier) : Subgroup G where
  carrier := H ‚à© K
  --EXERCISE
  nonempty := by
    simp [hHset, hKset]
    exact ‚ü®hH.nonempty, hK.nonempty‚ü©
  mul_closure := by
    intro a b ha hb
    simp [hHset, hKset] at *
    exact ‚ü®hH.mul_closure ha.left hb.left, hK.mul_closure ha.right hb.right‚ü©
  inv_closure := by
    intros a ha
    simp [hHset, hKset] at *
    exact ‚ü®hH.inv_closure ha.left, hK.inv_closure ha.right‚ü©

end Subgroups

end Defs
