import AlgebraInLean.World01.Sheet1

namespace AlgebraInLean

/- Groups aren't the only important structures in algebra. By adding or subtracting certain
properties from our definition, we create structures with different properties. -/

/- The simplest structure in algebra is a magma, which only requires a closed operation. -/
class Magma (G : Type*) where
  op : G ‚Üí G ‚Üí G

def Œº [Magma G] : G ‚Üí G ‚Üí G := Magma.op

/- The next structure is a semigroup, which is a magma with the associative property. The extends
keyword means that a semigroup has all the properties of a magma. -/
class Semigroup (G : Type*) extends Magma G where
  protected op_assoc : ‚àÄ a b c : G, Œº (Œº a b) c = Œº a (Œº b c)

theorem op_assoc [Semigroup G] (a b c : G) : Œº (Œº a b) c = Œº a (Œº b c) := Semigroup.op_assoc a b c

/- Next is a monoid, which has the properties of a semigroup, along with an identity. -/
class Monoid (G : Type*) extends Semigroup G where
  protected id : G
  protected op_id : ‚àÄ a : G, Œº a id = a
  protected id_op : ‚àÄ a : G, Œº id a = a

def ùïñ [Monoid G] : G := Monoid.id

theorem op_id [Monoid M] (a : M) : Œº a ùïñ = a := Monoid.op_id a

theorem id_op [Monoid M] (a : M) : Œº ùïñ a = a := Monoid.id_op a

/- Commutative monoids have a new property, the commutative property. This means that a * b = b * a
for any a or b. -/
class CommMonoid (G : Type*) extends Monoid G where
  protected op_comm : ‚àÄ a b : G, Œº a b = Œº b a

theorem op_comm [CommMonoid M] (a b : M) : Œº a b = Œº b a := CommMonoid.op_comm a b

/- Now, we can return to the definition of a group-/
class Group (G : Type*) extends Monoid G where
  protected inv : G ‚Üí G
  protected inv_op : ‚àÄ a : G, Œº (inv a) a = ùïñ

def Œπ [Group G] : G ‚Üí G := Group.inv

theorem inv_op [Group G] (a : G) : Œº (Œπ a) a = ùïñ := Group.inv_op a

/- The last structure is an Abelian group, which is a group with the commutative property. -/
class AbelianGroup (G : Type*) extends Group G, CommMonoid G

/- These are the definitions that will be used moving forwards. -/

/- Now, try to prove a theorem using the new definitions. -/
theorem left_cancel [Group G] (a b c : G) : Œº a b = Œº a c ‚Üí b = c := by
  -- sorry
  -- SAMPLE SOLUTION
  intro h
  rw[‚Üê(id_op b), ‚Üê(id_op c), ‚Üê(inv_op a), op_assoc, op_assoc, h]
  -- END OF SAMPLE SOLUTION
