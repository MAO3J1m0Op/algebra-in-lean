import AlgebraInLean.Chapter01.Sheet02

namespace AlgebraInLean

variable {Œ± : Type*}

/-
When we defined the monoids and groups, we only required the *existence* of identity and inverses,
but not their uniqueness. However, it does happen that they must be unique, as shown below.

It may be useful to write out the following theorems in standard math notation, like we did in the
previous theorems.
-/

/--
Uniqueness of the identity element in a monoid. If any element e‚ÇÇ "behaves like" the identity, then
it must be equal to the identity.
-/
theorem id_unique [Monoid Œ±] (e‚ÇÇ : Œ±) (h : ‚àÄ (a : Œ±), (Œº a e‚ÇÇ = a ‚àß Œº e‚ÇÇ a = a)) : e‚ÇÇ = ùïñ := by
  /-
  The `obtain` tactic may be useful. Given a hypothesis `h : P ‚àß Q`, `obtain ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© := h` creates
  hypotheses `h‚ÇÅ : P` and `h‚ÇÇ : Q`. This also works to destructure other types like `‚àÉ`.
  -/
  -- sorry
  -- SAMPLE SOLUTION
  specialize h ùïñ
  obtain ‚ü®h, _‚ü© := h -- we don't care about the property on the right, so we use `_` instead.
  rw [id_op] at h
  exact h
  -- END OF SAMPLE SOLUTION


/--
Uniqueness of the inverse of an element. If any element `i` "behaves like" the inverse of `a`, then
it must be equal to the inverse of `a`.
-/
theorem inv_unique [Group Œ±] (a i : Œ±) (h : Œº a i = ùïñ ‚àß Œº i a = ùïñ) : i = Œπ a := by
  -- sorry
  -- SAMPLE SOLUTION
  obtain ‚ü®h, _‚ü© := h
  rw [‚Üêop_id (Œπ a), ‚Üêh, ‚Üêop_assoc (Œπ a), inv_op, id_op]
  -- END OF SAMPLE SOLUTION

/- In fact, just the one-sided conditions of the above suffice! -/

/-- If any element `e‚ÇÇ` is a left identity, then it is equal to the identity -/
theorem id_unique_left [Monoid Œ±] (e‚ÇÇ : Œ±) (h : ‚àÄ (a : Œ±), Œº e‚ÇÇ a = a) : e‚ÇÇ = ùïñ := by
  -- sorry
  -- SAMPLE SOLUTION
  specialize h ùïñ
  rw [op_id] at h
  exact h
  -- END OF SAMPLE SOLUTION

/-- If any element `e‚ÇÇ` is a right identity, then it is equal to the identity -/
theorem id_unique_right [Monoid Œ±] (e‚ÇÇ : Œ±) (h : ‚àÄ (a : Œ±), Œº a e‚ÇÇ = a) : e‚ÇÇ = ùïñ := by
  -- sorry
  -- SAMPLE SOLUTION
  specialize h ùïñ
  rw [id_op] at h
  exact h
  -- END OF SAMPLE SOLUTION

/-- If any element `i` is a left inverse of `a`, then it is equal to the inverse of `a` -/
theorem inv_unique_left [Group Œ±] (a i : Œ±) (h : Œº i a = ùïñ) : i = Œπ a := by
  -- sorry
  -- SAMPLE SOLUTION
  rw [‚Üêid_op (Œπ a), ‚Üêh, op_assoc i, op_inv, op_id]
  -- END OF SAMPLE SOLUTION

/-- If any element `i` is a right inverse of `a`, then it is equal to the inverse of `a` -/
theorem inv_unique_right [Group Œ±] (a i : Œ±) (h : Œº a i = ùïñ) : i = Œπ a := by
  -- sorry
  -- SAMPLE SOLUTION
  rw [‚Üêop_id (Œπ a), ‚Üêh, ‚Üêop_assoc (Œπ a), inv_op, id_op]
  -- END OF SAMPLE SOLUTION

/-
Now that we have the uniqueness theorems, we can prove some more interesting theorems about the
identity and inverses.
-/

variable [Group Œ±]

/--
Colloquially, the "shoes and socks theorem" because you put on your socks before your shoes, but you
take off your shoes before your socks. "Anticommutativity" is the fancy name for this: a function
that "commutes" with the operation but inverts the order of the operands.
-/
theorem inv_anticomm (a b : Œ±) : Œπ (Œº a b) = Œº (Œπ b) (Œπ a) := by
  -- sorry
  -- SAMPLE SOLUTION
  symm
  apply inv_unique
  constructor
  ¬∑ rw [op_assoc, ‚Üêop_assoc b, op_inv, id_op, op_inv]
  ¬∑ rw [op_assoc, ‚Üêop_assoc (Œπ a), inv_op, id_op, inv_op]
  -- END OF SAMPLE SOLUTION

/-- The inverse of the inverse of an element is itself -/
theorem inv_inv (a : Œ±) : Œπ (Œπ a) = a := by
  --sorry
  -- SAMPLE SOLUTION
  symm
  apply inv_unique
  constructor
  ¬∑ exact inv_op a
  ¬∑ exact op_inv a
  -- END OF SAMPLE SOLUTION

/-- The inverse of the identity element is the identity element -/
theorem inv_id [Group Œ±] : Œπ ùïñ = (ùïñ : Œ±) := by
  -- sorry
  -- SAMPLE SOLUTION
  symm
  apply inv_unique
  constructor
  ¬∑ exact op_id ùïñ
  ¬∑ exact op_id ùïñ
  -- END OF SAMPLE SOLUTION
